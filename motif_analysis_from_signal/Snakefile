# This module takes a bed file that contains the signal regions from NGS genomic
# experiment, i.e. ChIP-seq, DNase-seq, etc and outputs the random selected
# subregions of it (the length is user-defined).

def get_output(config):
    out = []
    for f in config.keys():
        for i in range(config[f]['nrepeat']):
            out.append('output/{name}_ws-{window_size}_repeat-{i}.fa.gz'.format(name = f,
                window_size = config[f]['window_size'],
                i = i))
    return out

rule all:
    input:
        get_output(config)

rule bed2region:
    input:
        lambda wildcards: config[wildcards.name]['bed_path']
    output:
        'output/{name}_ws-{window_size}.region.txt.gz'
    shell:
        'python scripts/bed2region.py --bed {input[0]} --window_size {wildcards.window_size} \
        --output {output[0]}'

rule extract_region:
    input:
        'output/{name}_ws-{window_size}.region.txt.gz'
    params:
        nrg = lambda wildcards: config[wildcards.name]['nregion']
    output:
        'output/{name}_ws-{window_size}_repeat-{i}.bed.gz'
    shell:
        'python scripts/extract_region.py --region {input[0]} --output {output[0]} --nregion {params.nrg}'

rule region2seq:
    input:
        'output/{name}_ws-{window_size}_repeat-{i}.bed.gz'
    output:
        temp('output/{name}_ws-{window_size}_repeat-{i}.temp.fa')
    params:
        lambda wildcards: config[wildcards.name]['genomic_annotation']
    shell:
        'bedtools getfasta -fi {params[0]} -bed {input[0]} -fo {output[0]}'

rule uppercase:
    input:
        'output/{name}_ws-{window_size}_repeat-{i}.temp.fa'
    output:
        'output/{name}_ws-{window_size}_repeat-{i}.fa'
    shell:
        'python scripts/bed_uppercase.py --in {input[0]} --out {output[0]}'
